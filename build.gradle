plugins {
  id 'com.github.ben-manes.versions' version '0.47.0'
}

task archiveDeployment(type: Tar) {
  destinationDirectory = file('.')
  archiveFileName = 'deployment.tar'
  into 'deployment'
  from 'deployment'
}

// Set publishing version property globally for this project
configure(subprojects) {
    setProperty('publishing.version', {
      if (findProperty("version.isRelease").toBoolean()) {
        return "${findProperty("version.number")}"
      } else {
        def hash = 'git rev-parse --short HEAD'.execute().text.trim()
        return "${findProperty("version.number")}-SNAPSHOT-$hash"
      }
    }())
}

subprojects {
  apply plugin: 'com.github.ben-manes.versions'

  repositories {
    // Search the local filesystem before attempting remote repositories
    flatDir { dirs "$rootDir/third-party" }
    mavenCentral()
  }

  group = 'gov.nasa.jpl.aerie'

  tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
    options.encoding = 'UTF-8'
  }

  tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
  }
}
