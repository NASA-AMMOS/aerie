plugins {
  id 'java'
  id 'application'
  id 'com.github.node-gradle.node' version '5.0.0'
  id 'jacoco'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

node {
  nodeProjectDir = file("${project.projectDir.toPath().resolve("constraints-dsl-compiler")}")
  version = '20.12.1'
  download = true
}

task assembleConstraintsDSLCompiler(type: NpmTask) {
  dependsOn processResources
  dependsOn npmInstall
  args = ['--silent', 'run', 'build']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task generateDocumentation(type: NpmTask) {
  dependsOn npmInstall
  dependsOn processResources
  dependsOn assembleConstraintsDSLCompiler
  args = ['--silent', 'run', 'generate-doc']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task cleanDocsFromDocsDir(type: Delete) {
  delete rootDir.toPath().resolve('docs/edsl-api-docs/constraints-edsl-api/develop')
}

task publishDocs {
  dependsOn cleanDocsFromDocsDir
  dependsOn generateDocumentation
}

assemble {
  dependsOn assembleConstraintsDSLCompiler
  dependsOn generateDocumentation
}

test {
  dependsOn nodeSetup
  dependsOn assembleConstraintsDSLCompiler
  useJUnitPlatform {
    includeEngines 'jqwik', 'junit-jupiter'
  }

  // Add node bin directory to PATH, helps CI/CD services without node installed
  environment 'NODE_PATH', node.computedNodeDir.get().toString().concat("/bin/node")

  environment "CONSTRAINTS_DSL_COMPILER_ROOT", projectDir.toPath().resolve('constraints-dsl-compiler')
  environment "CONSTRAINTS_DSL_COMPILER_COMMAND", './build/main.js'

  testLogging {
    exceptionFormat = 'full'
  }
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

application {
  mainClass = 'gov.nasa.jpl.aerie.merlin.server.AerieAppDriver'
  applicationDefaultJvmArgs = ['-Xmx2g']
}

dependencies {
  implementation project(':merlin-driver')
  implementation project(':parsing-utilities')
  implementation project(':constraints')
  implementation project(':permissions')

  implementation 'io.javalin:javalin:5.6.3'
  implementation 'org.slf4j:slf4j-simple:2.0.7'
  implementation 'org.glassfish:javax.json:1.1.4'

  implementation 'org.postgresql:postgresql:42.6.1'
  implementation 'com.zaxxer:HikariCP:5.0.1'

  testImplementation project(':examples:foo-missionmodel')
  testImplementation project(':merlin-framework')
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
  testImplementation 'net.jqwik:jqwik:1.6.5'
}
