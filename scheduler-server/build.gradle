plugins {
  id 'java'
  id 'application'
  id 'com.github.node-gradle.node' version '3.3.0'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(18)
  }
}

node {
  nodeProjectDir = file("${project.projectDir.toPath().resolve("scheduling-dsl-compiler")}")
  version = '16.14.0'
  npmVersion = '8.5.0'
  download = true
}

task deleteConstraintsTypescript(type: Delete) {
  delete project.projectDir.toPath().resolve('scheduling-dsl-compiler/src/libs/constraints')
}

task copyConstraintsTypescript(type: Copy) {
  dependsOn deleteConstraintsTypescript
  from rootDir.toPath().resolve('merlin-server/constraints-dsl-compiler/src/libs')
  into project.projectDir.toPath().resolve('scheduling-dsl-compiler/src/libs/constraints')
}

clean {
  dependsOn deleteConstraintsTypescript
}

task assembleSchedulingDSLCompiler(type: NpmTask) {
  dependsOn copyConstraintsTypescript
  dependsOn processResources
  dependsOn npmInstall
  args = ['run', 'build']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task generateASTJsonSchema(type: NpmTask) {
  dependsOn processResources
  dependsOn npmInstall
  args = ['run', 'generate-ast-schema']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task generateDocumentation(type: NpmTask) {
  dependsOn npmInstall
  dependsOn processResources
  dependsOn copyConstraintsTypescript
  dependsOn assembleSchedulingDSLCompiler
  args = ['run', 'generate-doc']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

assemble {
  dependsOn assembleSchedulingDSLCompiler
  dependsOn generateASTJsonSchema
  dependsOn generateDocumentation
}

test {
  dependsOn(assembleSchedulingDSLCompiler)
  useJUnitPlatform()

  // Add node bin directory to PATH, helps CI/CD services without node installed
  projectDir.toPath().resolve('.gradle/nodejs').toFile().listFiles().each {
    environment 'NODE_PATH', "$it/bin/node"
  }

  if (System.getenv("NODE_PATH") == null) {
    environment 'NODE_PATH', "node"
  }

  environment "SCHEDULING_DSL_COMPILER_ROOT", projectDir.toPath().resolve('scheduling-dsl-compiler')
  environment "SCHEDULING_DSL_COMPILER_COMMAND", './build/main.js'

  dependsOn ":examples:banananation:assemble"
  environment 'AERIE_ROOT', rootDir.toString()
}

application {
  mainClass = 'gov.nasa.jpl.aerie.scheduler.server.SchedulerAppDriver'
  applicationDefaultJvmArgs = ['-Xmx2g']
}

dependencies {
  implementation project(':merlin-driver')
  implementation project(':parsing-utilities')
  implementation project(':contrib')
  implementation project(':constraints')
  implementation project(':scheduler')

  implementation project(':merlin-framework')

  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'io.javalin:javalin:4.6.0'
  implementation 'org.eclipse:yasson:1.0.5'
  implementation 'org.json:json:20220320'
  implementation 'org.apache.bcel:bcel:6.5.0'

  implementation 'com.impossibl.pgjdbc-ng:pgjdbc-ng:0.8.9'
  implementation 'com.zaxxer:HikariCP:5.0.1'

  testImplementation project(':examples:foo-missionmodel')
  testImplementation project(':merlin-framework')
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
  testImplementation 'org.assertj:assertj-core:3.23.1'
  testImplementation 'javax.json.bind:javax.json.bind-api:1.0'
  testImplementation 'org.glassfish:javax.json:1.1.4'
}


