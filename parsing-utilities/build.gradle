plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

test {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

dependencies {
  api 'org.glassfish:javax.json:1.1.4'
  api 'org.apache.commons:commons-lang3:3.13.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

publishing {
  publications {
    library(MavenPublication) {
      version = findProperty('publishing.version')
      from components.java
    }
  }

  publishing {
    repositories {
      maven {
        name = findProperty("publishing.name")
        url = findProperty("publishing.url")
        credentials {
          username = System.getenv(findProperty("publishing.usernameEnvironmentVariable"))
          password = System.getenv(findProperty("publishing.passwordEnvironmentVariable"))
        }
      }
    }
  }
}
