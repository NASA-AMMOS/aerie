plugins {
  id 'java'
  id 'application'
  id 'com.github.node-gradle.node' version '3.5.0'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(19)
  }

  withJavadocJar()
  withSourcesJar()
}

node {
  nodeProjectDir = file("${project.projectDir.toPath().resolve("scheduling-dsl-compiler")}")
  version = '18.13.0'
  download = true
}

task deleteConstraintsTypescript(type: Delete) {
  delete project.projectDir.toPath().resolve('scheduling-dsl-compiler/src/libs/constraints')
}

task copyConstraintsTypescript(type: Copy) {
  dependsOn deleteConstraintsTypescript
  dependsOn ':merlin-server:assembleConstraintsDSLCompiler'
  from rootDir.toPath().resolve('merlin-server/constraints-dsl-compiler/src/libs')
  into project.projectDir.toPath().resolve('scheduling-dsl-compiler/src/libs/constraints')
}

clean {
  dependsOn deleteConstraintsTypescript
}

task assembleSchedulingDSLCompiler(type: NpmTask) {
  dependsOn copyConstraintsTypescript
  dependsOn processResources
  dependsOn npmInstall
  args = ['run', 'build']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task generateASTJsonSchema(type: NpmTask) {
  dependsOn processResources
  dependsOn npmInstall
  args = ['run', 'generate-ast-schema']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task generateDocumentation(type: NpmTask) {
  dependsOn npmInstall
  dependsOn processResources
  dependsOn copyConstraintsTypescript
  dependsOn assembleSchedulingDSLCompiler
  args = ['run', 'generate-doc']
  inputs.files('package.json', 'package-lock.json', 'tsconfig.json')
  inputs.dir('src')
}

task cleanDocsFromDocsDir(type: Delete) {
  delete rootDir.toPath().resolve('docs/edsl-api-docs/scheduling-edsl-api/develop')
}

task publishDocs {
  dependsOn cleanDocsFromDocsDir
  dependsOn generateDocumentation
}

assemble {
  dependsOn assembleSchedulingDSLCompiler
  dependsOn generateASTJsonSchema
  dependsOn generateDocumentation
}

test {
  dependsOn(assembleSchedulingDSLCompiler)
  useJUnitPlatform()

  testLogging {
      exceptionFormat 'full'
  }

  // Add node bin directory to PATH, helps CI/CD services without node installed
  projectDir.toPath().resolve('.gradle/nodejs').toFile().listFiles().each {
    environment 'NODE_PATH', "$it/bin/node"
  }

  if (System.getenv("NODE_PATH") == null) {
    environment 'NODE_PATH', "node"
  }

  environment "SCHEDULING_DSL_COMPILER_ROOT", projectDir.toPath().resolve('scheduling-dsl-compiler')
  environment "SCHEDULING_DSL_COMPILER_COMMAND", './build/main.js'

  dependsOn ":examples:banananation:assemble"
  environment 'AERIE_ROOT', rootDir.toString()
}

application {
  mainClass = 'gov.nasa.jpl.aerie.scheduler.worker.SchedulerWorkerAppDriver'
  applicationDefaultJvmArgs = ['-Xmx2g']
}

// Link references to standard Java classes to the official Java 11 documentation.
javadoc.options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
javadoc.options.links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.9/'
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

dependencies {
  implementation project(':merlin-driver')
  implementation project(':scheduler-driver')
  implementation project(':scheduler-server')
  implementation project(':parsing-utilities')
  implementation project(':constraints')

  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'io.javalin:javalin:5.1.2'
  implementation 'org.slf4j:slf4j-simple:2.0.3'
  implementation 'org.eclipse:yasson:1.0.5'
  implementation 'org.apache.bcel:bcel:6.6.0'
  implementation 'com.impossibl.pgjdbc-ng:pgjdbc-ng:0.8.9'
  implementation 'com.zaxxer:HikariCP:5.0.1'

  testImplementation project(':examples:foo-missionmodel')
  testImplementation testFixtures(project(':scheduler-server'))
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
  testImplementation 'org.assertj:assertj-core:3.23.1'
  testImplementation 'junit:junit:4.12'
  testImplementation 'javax.json.bind:javax.json.bind-api:1.0'
  testImplementation 'org.glassfish:javax.json:1.1.4'
}
